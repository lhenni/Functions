grammar edu.uhdts.dsl.functions.FunctionsLanguage with org.eclipse.xtext.xbase.Xbase

generate functionsLanguage "http://www.uhdts.edu/dsl/functions/FunctionsLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

FunctionsFile:
	(namespaceImports=XImportSection)?
	(metamodelImports+=MetamodelImport)*
	(functionsImports+=FunctionsImport)*
	(functionsSegments+=FunctionsSegment)+;

FunctionsSegment:
	'functions:' name=ValidID
	(functions+=Function)*;

MetamodelImport:
	'import' package=[ecore::EPackage|STRING] 'as' name=ValidID (useQualifiedNames?='using qualified names')?;

FunctionsImport:
	'import functions from' functionsSegment=[FunctionsSegment] ('with prefix' prefix=ValidID)?;

Function:
	'function' name=ValidID '{'
		(statements+=FunctionStatement)*
	'}';

FunctionStatement:
	ExecuteStatement | FunctionCallStatement;

ExecuteStatement returns ExecuteStatement:
	{ExecuteStatement}
	'execute' CodeBlock;

FunctionCallStatement returns FunctionCallStatement:
	{FunctionCallStatement}
	FunctionCallBlock;

fragment CodeBlock:
	code=XExpression;

fragment FunctionCallBlock:
	'call' CodeBlock;
